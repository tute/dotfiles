#!/bin/zsh

action="cd"
base_folder="$HOME/Code/Buoy"

b() {
  local directories=($base_folder/*(/))
  local match_list=()
  local target=$1

  for dir in $directories; do
    local dir_base_name="${dir:t}"

    if [[ "${dir_base_name:l}" == *"${target:l}"* ]]; then
      match_list+=($dir)
    fi
  done

  if [[ -z "$target" ]]; then
    eval $action $base_folder
  elif [[ "${#match_list[@]}" -eq 1 ]]; then
    eval $action $match_list[1]
  elif [[ "${#match_list[@]}" -gt 1 ]]; then
    if command -v fzf &> /dev/null; then
      cd $(print -l $match_list | fzf)
    else
      echo "Multiple matches found:"
      print -l $match_list
    fi
  else
    echo "No matches found for '${target}'"
  fi
}

_b_completions() {
  _files -W $base_folder
}

compdef _b_completions b=cd
